* minimize drude particles
*

dimension maxres 2000000 maxaim 12000000 maxgrp 2000000

! Read topology and parameter files
set toppar ../toppar_drude
stream @toppar/toppar_drude_master_protein_2020b.str
stream @toppar/toppar_drude_nucleic_acid_2020b.str
stream @toppar/toppar_drude_lipid_2020b.str
!stream @toppar/toppar_drude_carbohydrate_2020b.str
stream @toppar/toppar_drude_model_2020b.str

! Read PSF and Coordinates
open read unit 10 card name ../@ion_@mol_files/@ion_@mol_@conc.psf
read psf  unit 10 card

open read unit 10 card name ../@ion_@mol_files/@ion_@mol_@conc.crd
read coor unit 10 card

coor sdrude
coor shake

SHAKE bonh param nofast -
      select  .not. type D*  end -
      select  .not. type D*  end

SET XTLTYPE  = ORTHO
SET A = 48
SET B = 48
SET C = 96
SET ALPHA = 90.0
SET BETA  = 90.0
SET GAMMA = 90.0
SET FFTX  = 60
SET FFTY  = 60
SET FFTZ  = 120
SET XCEN  = 0
SET YCEN  = 0
SET ZCEN  = 0

!open read unit 10 card name crystal_image.str
CRYSTAL DEFINE @XTLtype @A @B @C @alpha @beta @gamma
crystal build noper 0 cutoff 16.0
!CRYSTAL READ UNIT 10 CARD
!CRYSTAL BUILD CUTOFF 14.0

!Image centering by residue
IMAGE BYRESID XCEN @xcen YCEN @ycen ZCEN @zcen sele resname @ion .or. resname @mol end
IMAGE BYRESID XCEN @xcen YCEN @ycen ZCEN @zcen sele resname swm4 end

nbonds atom vatom switch vswitch bycb -
       ctonnb 10.0 ctofnb 12.0 cutnb 16.0 cutim 16.0 -
       inbfrq -1 imgfrq -1 wmin 1.0 cdie eps 1.0 -
       ewald pmew fftx @fftx ffty @ffty fftz @fftz  kappa .34 spline order 6

energy

! first minimize drudes, then entire system
cons harm force 100000. sele .not. type D* end
mini SD   nstep 100 nprint 20
!mini ABNR nstep 100 nprint 20
cons harm force 0.0 sele all end

! minimize all atoms and drudes
mini SD   nstep 100 nprint 20
!mini ABNR nstep 100 nprint 20

!write coor pdb name ../@ion_@mol_files/@ion_@mol_@conc.mini.pdb
!write coor card name ../@ion_@mol_files/@ion_@mol_@conc.mini.crd

!stop

set temp  = 298.15
DrudeHardWall L_WALL 0.2

! TPcontrol
! -------------------------------------------------
TPCONTROL NTHER 2  CMDAMP 10.0  NSTEP 20  -
  THER 1 TAU  0.1   TREF @temp   SELECT all .and. .NOT. TYPE D* END  -
  THER 2 TAU  0.005 TREF 1.00   SELECT TYPE D* END -
  BARO   BTAU 0.1   PREF 1.00 DSCY
      
! Run equilibration and Output for equilibration
! -------------------------------------------------
open unit 20 write unform name ../@ion_@mol_files/@ion_@mol_@conc.dcd
open unit 22 write form name ../@ion_@mol_files/@ion_@mol_@conc.res

DYNAMICS  vv2      start    timestep 0.0005 nstep 2000 -
	  ntrfrq    1000   iprfrq   -1  -
	  nprint    1000     iseed    54321 -
	  iasvel       1     firstt   @temp   finalt    @temp    -
	  inbfrq      -1     imgfrq   -1      ihbfrq    0       ilbfrq     0  -
	  iunread     -1     kunit -1 -
          iunwrite    22  -
          iuncrd      20     nsavcrd   1000

write coor pdb name ../@ion_@mol_files/@ion_@mol_@conc.mini.pdb
write coor card name ../@ion_@mol_files/@ion_@mol_@conc.mini.crd

stop