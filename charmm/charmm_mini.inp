* minimize drude particles
*

dimension maxres 2000000 maxaim 12000000 maxgrp 2000000

! Read topology and parameter files
stream toppar_drude.str

! Read PSF and Coordinates
open read unit 10 card name ../run_@mol1_@mol2/@mol1_@mol2_@conc.psf
read psf  unit 10 card

open read unit 10 card name ../run_@mol1_@mol2/@mol1_@mol2_@conc.crd
read coor unit 10 card

coor sdrude
coor shake

SHAKE bonh param tolerance 1.0e-9 nofast -
      select ( .not. (type D*)) end -
      select ( .not. (type D*)) end noreset

SET XTLTYPE  = ORTHO
SET A = 48
SET B = @A
SET C = 96
SET ALPHA = 90.0
SET BETA  = 90.0
SET GAMMA = 90.0
SET FFTX  = 54
SET FFTY  = 54
SET FFTZ  = 108
SET XCEN  = 0
SET YCEN  = 0
SET ZCEN  = 0

coor stat

CRYSTAL DEFINE @XTLtype @A @B @C @alpha @beta @gamma
crystal build noper 0 cutoff 16.0

!Image centering by residue
IMAGE BYRESID XCEN @xcen YCEN @ycen ZCEN @zcen sele resname @mol1 .or. resname @mol2 end
IMAGE BYRESID XCEN @xcen YCEN @ycen ZCEN @zcen sele resname swm4 end

nbonds atom vatom switch vswitch -
       ctonnb 10.0 ctofnb 12.0 cutnb 16.0 cutim 16.0 -
       inbfrq -1 imgfrq -1 ihbfrq 0 wmin 1.0 cdie eps 1.0 -
       ewald pmew fftx @fftx ffty @ffty fftz @fftz  kappa .34 spline order 6

energy

! first minimize drudes
cons harm force 10000000. sele .not. type D* end
mini SD   nstep 50 nprint 10
cons harm force 0.0 sele all end

cons harm force 10000000. sele .not. ( type D* .or. resname SWM4 ) end
mini SD   nstep 50 nprint 10
cons harm force 0.0 sele all end

! minimize all atoms and drudes
mini SD   nstep 100 nprint 10

write coor pdb name ../run_@mol1_@mol2/@mol1_@mol2_@conc.mini.pdb
write coor card name ../run_@mol1_@mol2/@mol1_@mol2_@conc.mini.crd

stop

set temp  = 303.15
DrudeHardWall L_WALL 0.2

! TPcontrol
! -------------------------------------------------
TPCONTROL NTHER 2  CMDAMP 10.0  NSTEP 20  -
  THER 1 TAU  0.1   TREF @temp   SELECT all .and. .NOT. TYPE D* END  -
  THER 2 TAU  0.005 TREF 1.00   SELECT TYPE D* END -
  BARO   BTAU 0.1   PREF 1.00 DSCY
      
! Run equilibration and Output for equilibration
! -------------------------------------------------
open unit 20 write unform name ../run_@mol1_@mol2/@mol1_@mol2_@conc.equi.dcd
open unit 22 write form name ../run_@mol1_@mol2/@mol1_@mol2_@conc.equi.res

DYNAMICS  vv2      start    timestep 0.0005 nstep 2000 -
	  ntrfrq    500   iprfrq   -1  -
	  nprint    100     iseed    54321 -
	  iasvel       1     firstt   @temp   finalt    @temp    -
	  inbfrq      -1     imgfrq   -1      ihbfrq    0       ilbfrq     0  -
	  iunread     -1     kunit -1 -
          iunwrite    22  -
          iuncrd      20     nsavcrd   500

write coor pdb name ../run_@mol1_@mol2/@mol1_@mol2_@conc.mini.pdb
write coor card name ../run_@mol1_@mol2/@mol1_@mol2_@conc.mini.crd

stop